set terminal cairolatex pdf colortext color size 10cm, 8cm header '\newcommand{\etiqueta}[1]{\setlength{\fboxsep}{0.75pt}\colorbox{white}{#1}}'
set output 'calors.tex'

# Format dels eixos
set xlabel '$\mathsfit t$ (\si{h})' offset 0,-1
set ylabel '$\mathsfit q$ (\si{\celsius.s^{-1}})' offset -3,0
set format '\num{%g}'
set border linewidth 1.5
set lmargin at screen 0.2
set rmargin at screen 0.99

set style line 1 linecolor rgb "dark-violet" linewidth 1.5
set style line 2 linecolor rgb "dark-orange" linewidth 1.5 
set style line 4 linecolor rgb "forest-green" linewidth 1.5 
set style line 5 linecolor rgb "navy-blue" linewidth 1.5 
set style line 3 linecolor rgb "gray" linewidth 1 dashtype 2

set xrange [0:24]
set yrange [0:2]
set samples 10000
set xtics scale 1,0.5 offset 0,-0.5
set mxtics 2
set ytics scale 1,0.5 offset -0.5,0
set mytics 2
set grid xtics ytics mxtics mytics linestyle 3

set label '\etiqueta{\normalsize $\mathsfit q_\text{1}$}' at 2.5,1.03 center textcolor rgb "dark-violet" front
set label "\\etiqueta{\\normalsize $\\mathsfit q'_\\text{1}$}" at 2.5,0.8 center textcolor rgb "forest-green" front
set label '\etiqueta{\normalsize $\mathsfit q_\text{2}$}' at 2.5,0.45 center rotate by -45 textcolor rgb "dark-orange" front

k = 0.1
w = pi/12
Tb = 0.5*(20 + 8)
DT = (20 - 8)
q1 = k*(22 - Tb) + k**2*DT/(2*sqrt(k**2 + w**2))
q2(t) = k*(22 - Te(t))
q3 = k*(22 - Tb)
Te(t) = Tb + 0.5*DT*sin(w*t)
T(t) = 18*exp(-k*t) + (1 - exp(-k*t))*(q/k + Tb) + k*0.5*DT*(k*sin(w*t) - w*cos(w*t) + w*exp(-k*t))/(k**2 + w**2)

plot q1 notitle with lines linestyle 1, q2(x) notitle with lines linestyle 2, q3 notitle with lines linestyle 4 
